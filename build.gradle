plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'checkstyle'
    id 'jacoco'
    id 'com.github.spotbugs' version '4.7.10'
    id 'com.diffplug.spotless' version '6.0.0'
    id "io.github.gradle-nexus.publish-plugin"  version "1.1.0"
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

version = '3.0.5'

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.slf4j:slf4j-api:1.7.32'
    testImplementation 'ch.qos.logback:logback-classic:1.0.13'
    testImplementation 'junit:junit:4.13.2'
}

checkstyle {
    toolVersion = '8.8'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

javadoc {
    failOnError = false
}

task javadocJar(type: Jar) {
    from javadoc.outputs
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId 'org.omegat'
            artifactId 'vldocking'
            version version

            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components["java"])
            pom {
                name.set("vldocking")
                description.set("vldocking")
                url.set("https://github.com/omegat-org/vldocking")
                licenses {
                    license {
                        name.set("The GNU Lesser General Public License, Version 3")
                        url.set("https://www.gnu.org/licenses/licenses/lgpl-3.html")
                        distribution.set("repo")
                    }
                }
                developers {
                    developer {
                        id.set("miurahr")
                        name.set("Hiroshi Miura")
                        email.set("miurahr@linux.com")
                    }
                }
                scm {
                    connection.set("scm:git:git://github.com/omegat-org/vldocking.git")
                    developerConnection.set("scm:git:git://github.com/omegat-org/vldocking.git")
                    url.set("https://github.com/omegat-org/vldocking")
                }
            }
        }
    }
}

signing {
    if (!project.hasProperty("signingKey")) {
        useGpgCmd()
    }
    sign(publishing.publications["mavenJava"])
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username.set(System.getenv("SONATYPE_USER"))
            password.set(System.getenv("SONATYPE_PASS"))
        }
    }
}